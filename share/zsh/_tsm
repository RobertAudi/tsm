#compdef tsm
#autoload

local context state line curcontext="$curcontext" ret=1
local -a cmds
cmds=(
  'list:List saved sessions'
  'show:Show details about a session'
  'save:Save the current session'
  'remove:Remove a saved session'
  'rename:Rename a saved session'
  'copy:Copy a saved session'
  'duplicate:Duplicate a saved session'
  'restore:Restore a saved session'
  'resume:Restore and attach a saved session'
  'quit:Quit tmux'
  'version:Show version'
  'help:Show usage information'
)

_arguments -C \
  '1:tsm command:->subcommand' \
  '*:: :->args' \
  && ret=0

case $state in
  subcommand)
    _describe -t commands 'tsm commands' cmds && ret=0
    ;;
esac

case "$line[1]" in
  show|remove|rename|copy|duplicate|restore|resume)
    local sessions_dir
    local -a session_files
    sessions_dir="${TSM_SESSIONS_DIR:-${TSM_HOME:-${XDG_DATA_HOME:-$HOME/.local/share}/tsm}/sessions}"
    session_files=("${sessions_dir}"/*.txt(.NOmf:gu+r::t:r))

    if (( ${#session_files} == 0 )); then
      _message -e 'no sessions found' && ret=1
    else
      _values 'Sessions' $session_files && ret=0
    fi
    ;;
  help)
    _describe -t commands 'tsm commands' cmds && ret=0
    ;;
  list|quit|version)
    _message -e 'no more arguments' && ret=1
    ;;
esac

return ret
